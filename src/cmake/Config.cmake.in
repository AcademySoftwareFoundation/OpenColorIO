@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if (NOT @BUILD_SHARED_LIBS@) # NOT @BUILD_SHARED_LIBS@
    if (APPLE)
        # Store the previous value of CMAKE_FIND_FRAMEWORK and CMAKE_FIND_APPBUNDLE.
        set(_PREVIOUS_CMAKE_FIND_FRAMEWORK ${CMAKE_FIND_FRAMEWORK})
        set(_PREVIOUS_CMAKE_FIND_APPBUNDLE ${CMAKE_FIND_APPBUNDLE})

        # Prioritize other paths before Frameworks and Appbundle for find_path, find_library and 
        # find_package.
        set(CMAKE_FIND_FRAMEWORK LAST)
        set(CMAKE_FIND_APPBUNDLE LAST)
    endif()

    # Append OCIO custom find module path.
    list(APPEND CMAKE_MODULE_PATH "${PACKAGE_PREFIX_DIR}/share/OpenColorIO/cmake/modules")
    list(APPEND CMAKE_MODULE_PATH "${PACKAGE_PREFIX_DIR}/share/OpenColorIO/cmake/macros")

    ########################
    # Required dependencies 
    ########################

    if (NOT TARGET expat::expat)
        find_dependency(expat @expat_VERSION@)
    endif()

    if (NOT TARGET Imath::Imath)
        find_dependency(Imath @Imath_VERSION@)
    endif()

    if (NOT TARGET pystring::pystring)
        find_dependency(pystring @pystring_VERSION@)
    endif()

    if (NOT TARGET yaml-cpp::yaml-cpp)
        find_dependency(yaml-cpp @yaml-cpp_VERSION@)
        if (TARGET yaml-cpp AND NOT TARGET yaml-cpp::yaml-cpp)
            add_library(yaml-cpp::yaml-cpp ALIAS yaml-cpp)
        endif()
    endif()

    if (NOT TARGET ZLIB::ZLIB)
        # ZLIB_VERSION is available starting CMake 3.26+.
        # ZLIB_VERSION_STRING is still available for backward compatibility.
        # See https://cmake.org/cmake/help/git-stage/module/FindZLIB.html

        if (@ZLIB_VERSION@) # @ZLIB_VERSION@
            find_dependency(ZLIB @ZLIB_VERSION@)
        else()
            find_dependency(ZLIB @ZLIB_VERSION_STRING@)
        endif()
    endif()

    if (NOT TARGET MINIZIP::minizip-ng)
        find_dependency(minizip-ng @minizip-ng_VERSION@)
    endif()

    # Remove OCIO custom find module path.
    list(REMOVE_AT CMAKE_MODULE_PATH -1)

    if (APPLE)
        # Restore CMAKE_FIND_FRAMEWORK and CMAKE_FIND_APPBUNDLE values.
        set(CMAKE_FIND_FRAMEWORK ${_PREVIOUS_CMAKE_FIND_FRAMEWORK})
        set(CMAKE_FIND_APPBUNDLE ${_PREVIOUS_CMAKE_FIND_APPBUNDLE})
    endif()
endif()

include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake)

include(FindPackageHandleStandardArgs)
set(@PROJECT_NAME@_CONFIG ${CMAKE_CURRENT_LIST_FILE})
find_package_handle_standard_args(@PROJECT_NAME@ CONFIG_MODE)
